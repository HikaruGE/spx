module context/declare

imports
	context/sdf/declare
	context/str/declare
	context/esv/declare
	context/template/declare
	context/sdf/declare-signature
	context/-
	normalize/signatures
imports
	include/SpoofaxLang
	lib/editor-common.generated
imports
	utils/annotations
	utils/debug
	utils/contract
	config
	common

rules
	declare-package =
		contracts(
			contract-requires(
				?Package(QName(_))
			)
		);
		get-enclosed-modules;
		map(get-module-declaration);
		list-loop(declare-internal(declare-global-symbols-main))

	
	delcare-module =  
		declare-internal(declare-local-symbols-main)
	
	declare-local-symbols-main = 
		declare-spxmodules-all(declare-local-symbols); 
		update-modified-module-declaration

	declare-global-symbols-main = 
		declare-spxmodules-all(declare-global-symbols) ;  
		update-modified-module-declaration

	declare-internal(s) = 
		origin-track-forced(s)
	
	init-declaration = 
		init-record-signatures ; 
		init-template-options


rules

	declare-spxmodules-all(s):
		Module(attrb* , modname , section*) -> Module(attrb* , modname , m-sections*)
		where
			<alltd(s)>section* => m-sections*

	update-modified-module-declaration:
		m@Module(attrb* , modname , section*) -> m
		with
				Package( package-qname) := <get-annotation(get-package-annotation)>modname
		with
			<store-declaration(|Modules())>(<get-module-untyped-qname>(package-qname, modname),  m)

rules
	declare-local-symbols:
		SDFSection(definition*) -> SDFSection(declared-definition*)
		where
			declared-definition* := <declare-sdf-definition-main>definition*

	declare-local-symbols:
		SDFSectionEH(_,definition*) -><declare-local-symbols>SDFSection(definition*)

	declare-local-symbols:
	  STRSection(def*) -> STRSection(m-definition*)
    where
      m-definition* := <declare-str-definition-main>def*

  declare-local-symbols: ESVSection(def*) -> <id>

  // TemplateLang
  declare-local-symbols:
    TemplateSection(definition*) -> TemplateSection(declared-definition*)
    with
      declared-definition* := <declare-template-definition-main> definition*

  // TemplateLang
  // FIXME: move to spoofax.configuration?
  declare-local-symbols:
  	TemplateOptions(option*) -> TemplateOptions(declared-option*)
  	with
  	  declared-option* := <declare-template-options-main> option*

rules

	declare-global-symbols: SDFSection(def*) -> <id>

	declare-global-symbols: SDFSectionEH(_,def*) -> <id>

	declare-global-symbols: s@STRSection(def*) -> STRSection(m-definition*)
	where
		m-definition* := <declare-stratego-globals-top>def*

	declare-global-symbols: ESVSection(def*) -> ESVSection(declared-definition*)
	where 
		declared-definition* := <declare-esv-globals-top>def*
	
  // TemplateLang
  declare-global-symbols: TemplateSection(def*) -> <id>

  // TemplateLang
  declare-global-symbols: TemplateOptions(opt*) -> <id>

rules

	// Declaring current scope for further analyze
	declare-current-scope(| package-typed-qname , module-typed-qname)=
		with(
			<declare-current-package-scope>package-typed-qname;
			<declare-current-module-scope>module-typed-qname
		)

	declare-current-package-scope :
		package-typed-qname -> <id>
		with
			rules ( GetEnclosingPackageName      := package-typed-qname)

	declare-current-module-scope:
			module-typed-qname -> <id>
			with
				rules ( GetEnclosingModuleName      := module-typed-qname)

rules

	declare-legacy-artifact(|resolved-path) : legacy-content -> legacy-content
	where
		<has-extension(|"sdf") + has-extension(|"def")>resolved-path;
		<alltd(declare-sdf-definition-main)>legacy-content

	declare-legacy-artifact(|resolved-path) : legacy-content -> legacy-content
	where
		<has-extension(|"str") + has-extension(|"rtree")>resolved-path;
		<alltd(declare-str-definition-main)>legacy-content
