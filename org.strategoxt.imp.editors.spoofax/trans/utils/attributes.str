module utils/attributes
imports
	libstratego-lib
	libstratego-sglr
	include/SpoofaxLang

signature
  constructors
    deprecated : String -> Term
    deprecated : Term

overlays
  sdf-cons(x) = term(default(appl(unquoted("cons"), [fun(quoted(x))])))
  sdf-deprecated() = term(default(fun(unquoted("deprecated"))))
  sdf-deprecated(x) = term(default(appl(unquoted("deprecated"), [fun(quoted(x))])))

strategies

  fetch-cons =
    ?sdf-cons(_) + ?Constructor(_)

  fetch-cons:
    attrs(attr*) -> <getfirst(fetch-cons)> attr*

  fetch-cons-name:
    sdf-cons(cons) -> <un-double-quote> cons

  fetch-cons-name:
    Constructor(cons) -> <un-double-quote> cons

  fetch-cons-name:
    attrs(attr*) -> <getfirst(fetch-cons-name)> attr*

  is-bracket:
    attrs(attr*) -> <one(?bracket())> attr*

  is-deprecated:
    attrs(attr*) -> <one(?deprecated(_) + ?deprecated()
                         + ?sdf-deprecated() + ?sdf-deprecated(_))> attr*
