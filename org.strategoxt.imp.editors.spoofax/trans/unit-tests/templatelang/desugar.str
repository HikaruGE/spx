module desugar

imports
  libstratego-lib
  include/SpoofaxLang
  normalize/templatelang
  utils/attributes

strategies

  test-templatelang-desugar =
    test-suite(!"templatelang-desugar",
      apply-test(!"  transforms NoOptions() into Options([])",
        templatelang-desugar, !NoOptions(), !Options([]));
      apply-test(!"  transforms no-attrs() into attrs([])",
        templatelang-desugar, !no-attrs(), !attrs([]));
      apply-test(!"  replaces tabs with two spaces",
        templatelang-desugar, !Layout("\t"), !Layout("  "));
      apply-test(!"  unescapes common escape chars",
        templatelang-desugar, !Escape("\\ \\'\\\"\\t\\r\\n"), !String(" '\"\t\r\n"));
      apply-test(!"  unescapes escaped brackets",
        templatelang-desugar, !Escape("\\<\\>\\[\\]"), !String("<>[]"));
      apply-test(!"  unescapes unicode codepoints",
        templatelang-desugar, !Escape("\\u41\\u61"), !String("Aa"));
      apply-test(!"  un-double-quotes and unescapes separators",
        templatelang-desugar, !Separator("\"\\n\""), !Separator("\n"))
    );
    test-suite(!"templatelang-desugar-sdf",
      apply-test(!"  transforms SdfProductionWithCons without attrs into prod",
        templatelang-desugar-sdf,
        !SdfProductionWithCons(SortCons("X", "Y"), [], no-attrs()),
        !prod([], sort("X"), attrs([sdf-cons("\"Y\"")]))
      );
      apply-test(!"  transforms SdfProductionWithCons with attrs into prod",
        templatelang-desugar-sdf,
        !SdfProductionWithCons(SortCons("X", "Y"), [], attrs([assoc(left())])),
        !prod([], sort("X"), attrs([sdf-cons("\"Y\""), assoc(left())]))
      );
      apply-test(!"  transforms SdfProduction into prod",
        templatelang-desugar-sdf,
        !SdfProduction("X", [], no-attrs()),
        !prod([], sort("X"), no-attrs())
      )
    )
