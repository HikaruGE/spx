module templatelang-to-sdf

language SpoofaxLang
target language SDF

setup [[
  package example
  module example
  templates
]]

target setup header [[
  module example
  exports
    context-free syntax
]]

test empty [[
  X.Y = <<>>
  Z.Y = <<>>  // double test to be able to re-use the footer for this test
]]
build test-templatelang-to-sdf to [[
  -> X {cons("Y")}
  -> Z {cons("Y")}
]]

test placeholder [[
  X.Y = <<<Z>>>
]]
build test-templatelang-to-sdf to [[
  Z -> X {cons("Y")}
]]

test placeholder (text) [[
  X.Y = <<<this text is ignored : Z>>>
]]
build test-templatelang-to-sdf to [[
  Z -> X {cons("Y")}
]]

test placeholder (option) [[
  X.Y = <<<Z?>>>
]]
build test-templatelang-to-sdf to [[
  Z? -> X {cons("Y")}
]]

test placeholder (plus) [[
  X.Y = <<<Z+>>>
]]
build test-templatelang-to-sdf to [[
  Z+ -> X {cons("Y")}
]]

test placeholder (star) [[
  X.Y = <<<Z*>>>
]]
build test-templatelang-to-sdf to [[
  Z* -> X {cons("Y")}
]]

test placeholder (plus, separator) [[
  X.Y = <<<Z+; separator=", ">>>
]]
build test-templatelang-to-sdf to [[
  {Z ","}+ -> X {cons("Y")}
]]

test placeholder (star, separator) [[
  X.Y = <<<Z*; separator=" + ">>>
]]
build test-templatelang-to-sdf to [[
  {Z "+"}* -> X {cons("Y")}
]]

target setup footer [[
  context-free syntax
    CONTENTCOMPLETE -> X {cons("WATER"), avoid}
    CONTENTCOMPLETE -> Z {cons("WATER"), avoid}
]]
