module sdf/to-sdf

imports
  libstratego-lib
  libstratego-gpp
  libstratego-sglr
  libstratego-sdf
imports
	include/SpoofaxLang
	lib/editor-common.generated
  lib/sdf-desugar
  lib/sdf-parenthesize  
imports 
	sdf/analysis  
imports 
	common
	spoofaxlang
      
rules
  
  /**
  * Converting def to SDF definition. 
  */
  to-sdf:
    (def, path , project-path) -> 'module(name, [sdf-import-stmt*], [exports(sections')])
    with 
    	import-stmt* := <collect-om(?Imports(<id>) , conc);flatten-list;make-set> def
    with
      (name, relative-autogen-path) := <find-spoofax-module-name-string>(path, def)
      ;sdf-import-stmt* := 'imports( <map(spximport-to-sdfimport)>import-stmt* )
      ;sections  := <collect-om(?SDFSection(<id>) + ?SDFSectionEH(_, <id>), conc)> def
      ;sections' := <topdown(repeat(desugar-sdf-redux))> <list-to-conc-grammars> sections
 
 rules
 
 //TODO : package-wide sdf generation on SAVE 
 //i.e. get the package name , find all the sdf definition of the package 
 //generate sdf definition 
	to-sdf-all: (def, path , project-path)  -> None()
 		with 
 			declared-concept-entries := <get-all-declared-entries>;
 		  (name, relative-autogen-path) := <find-spoofax-module-name-string>(path, def)
 		where 
 			<debug>$[Namespace-reference-table : [<pp-aterm>declared-concept-entries]]
 		where
 			<map-declarations(to-sdf(|project-path))>declared-concept-entries
			;generate-sdf-main(|project-path , name, relative-autogen-path)
 	
 	
 	to-sdf(|project-path):
		(package-name,concept-name,def) -> sdf-string
		with 
			autogen-directory := $[[<Autogenerated-Artifacts-Dir>]/[<Package-Gen-Root-of-DeclaredConcept>(package-name,concept-name)]]
			;debug(!"autogen-directory : ") 
		with 
    	import-stmt* := <collect-om(?Imports(<id>) , conc);flatten-list;make-set> def
    	;name := <get-qualified-spxmodulename>(package-name,concept-name)
    	;output-path := <get-fullyqualified-autogen-path(|"sdf")>(project-path,autogen-directory , name)
    	;<debug>$[Gen Sdf for : [name] path : [output-path]] 
    	;<debug>output-path  
    with
      sdf-import-stmt* := 'imports( <map(spximport-to-sdfimport)>import-stmt* )
      ;sections  := <collect-om(?SDFSection(<id>) + ?SDFSectionEH(_, <id>), conc)> def
      ;debug(!"sdf-sections : ") 
      ;sections' := <alltd(desugar-sdf-redux)> <list-to-conc-grammars> sections
 		where 
 			sdf-string := <pp-sdf-string'> 'module(<get-qualified-importpath>(package-name,concept-name), [sdf-import-stmt*], [exports(sections')])
 		where 
			<ensure-exists>(project-path, autogen-directory) 
			;<save-to-file>(output-path , sdf-string )
	
	
	generate-sdf-main(|project-path , main-package-name,relative-autogen-path)=
		  declared-concept-entries := <get-all-declared-entries>
		  ;main-sdf-file-name := $[[main-package-name]-main]
		  ;sdf-string := 
       $[module [relative-autogen-path]/[main-sdf-file-name]
         imports
	         [<project-allkeys-declared(to-sdf-import)>declared-concept-entries]   									 
        ]
			;autogen-dir :=$[[<Autogenerated-Artifacts-Dir>]/[relative-autogen-path]]
			;output-path := <get-fullyqualified-autogen-path(|"sdf")>(project-path, autogen-dir , main-sdf-file-name)
			;<ensure-exists>(project-path, autogen-dir) 
			;<save-to-file>(output-path , sdf-string )
	 					

rules

	override pp-sdf-string = fail // ensure compiler doesn't blow up
	
  pp-sdf-string' =
    sdf-desugar; 
    parenthesize-Sdf2; 
  	sdf-ppfix; 
    ast2box(|[<pp-table-sdf>]); 
    box2text-string(|80)			 

rules
  // TODO: also support Constructor("Module(<2>, <1>)") => {ast("...")}?
  //desugar-sdf-redux:
  //  term(default(fun(quoted(s)))) -> term(default(appl(unquoted("cons"), [fun(quoted(s))])))

  desugar-sdf-redux:
    Constructor(s) -> term(default(appl(unquoted("cons"), [fun(quoted(s))])))

  desugar-sdf-redux:
    namespacedef(_, s) -> s

  desugar-sdf-redux:
    namespaceref(n) -> sort(s')
    where
      s' := <NamespaceSort <+ !"ID"> n

  desugar-sdf-redux:
    namespaceref2(n, s) -> s
  
  list-to-conc-grammars:
    [g1, g2 | gs] -> conc-grammars(g1, <list-to-conc-grammars> [g2 | gs])
  
  list-to-conc-grammars:
    [g1] -> g1
  
  list-to-conc-grammars:
    [] -> []
    
rules 
	
	/**
	* Converts import statements of SPX to import statements of SDF
	*/
	spximport-to-sdfimport :  
			Import(Name( package-name) ) -> 'module( unparameterized(package-name))
			

	to-sdf-import:(p-name , c-name) -> result 
		where 
				result := $[[<get-qualified-importpath>(p-name,c-name)]
									 ]
	